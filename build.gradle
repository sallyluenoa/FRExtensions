// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        // App Build Config
        buildConfig = [
                applicationId: 'frextensions',
                applicationName: 'FRExtensions',
                versionName: project.findProperty("version.name") ?: '0.0.1-SNAPSHOT',
                versionCode: project.findProperty("version.code") as Integer ?: 1,
                buildTools: "30.0.2",
                compileSdk: 30,
                targetSdk: 30,
                minSdk: 21,
        ]
        // Library Versions
        versions = [
                // @see https://developer.android.com/studio/releases/gradle-plugin
                androidGradle: '7.0.0',
                // @see https://kotlinlang.org/
                kotlin: '1.5.21',
                // @see https://github.com/Kotlin/dokka
                dokka: '1.5.0',
                // @see https://developer.android.com/jetpack/androidx/versions
                androidx: [
                        appcompat: '1.3.1',
                        constraintlayout: '2.1.0',
                        core: '1.6.0',
                        testCore: '1.4.0',
                        testEspresso: '3.4.0',
                        testJunit: '1.1.3',
                ],
                // @see https://mvnrepository.com/artifact/com.google
                google: [
                        androidMaterial: '1.4.0',
                        truth: '1.1.3',
                ],
                // @see https://junit.org/junit4/
                junit: '4.13.2',
        ]
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$versions.androidGradle"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$versions.dokka"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}